***************************************************************************************************
**  Project: 	Code for "Interrupted time series analysis using Autoregressive 	         **
**			Integrated Moving Average (ARIMA) models: A guide for evaluating         **
**			large-scale health interventions"				         **
**  Author:	Dr Andrea Schaffer							         **
** 		Centre for Big Data Research in Health				                 **
**		UNSW Sydney							                 **
**  Last updated 2 July 2020									 **
***************************************************************************************************;

* Load data;
proc import out=quet 
            datafile="quet.csv" 
            dbms=csv replace;
     getnames=yes;
     datarow=2; 
run;

* View data;
proc print data=quet; run;

* Plot data to visualise time series;
symbol1 interpol=join;
axis1 label=("Month");
axis2 label=(angle=90 "Dispensings") order=(0 to 40000 by 5000);
proc gplot data=quet;
	plot dispensings*month / href='1jan2014'd lhref=4 chref="lightgray" haxis=axis1 vaxis=axis2; 
run; 
quit;
	 
* View ACF/PACF plots of undifferenced data;
proc arima data=quet;
	identify var=dispensings nlag=24;
run; 
quit;

* View ACF/PACF plots of differenced/seasonally differenced data;
proc arima data=quet;
	identify var=dispensings(1,12) nlag=24;
run; 
quit;

* Create variable representing step change and ramp and view;
data quet; set quet;
	step=(month>='1jan2014'd);
	ramp=intck('month','1dec2013'd,month);
		if month<'1jan2014'd then ramp=0; 
run;
proc print data=quet; 
run;

* Specify first difference = 1 and seasonal difference = 1;
* Run model and check residuals (note: q=(12) is equivalent to q=0/Q=1);
proc arima data=quet;
	identify var=dispensings(1,12) crosscorr=(step(1,12) ramp(1,12));
	estimate p=2 q=(12) input=(step ramp) noint method=ml outmodel=out; 
run; 
quit;

* Get confidence intervals;
data out; set out;
	lci=_value_-1.96*_std_;
	uci=_value_+1.96*_std_; 
run;

* To forecast the counterfactual, model data excluding post-intervention time period;
data quet2; set quet;
	if month>='1jan2014'd then dispensings=.;
run;

* Forecast 12 months post-intervention;
proc arima data=quet2;
	identify var=dispensings(1,12);
	estimate p=2 q=0 noint;
	forecast lead=12 interval=month id=month out=forecast ;
run; 
quit;

* Combine with observed data;
data quet3; merge quet forecast (keep=month forecast);
	by month; 
	if month<'1jan2014'd then forecast=.; 
run;
proc print data=quet3; 
run;

* Plot;
symbol1 interpol=join c=blue;
symbol2 interpol=join c=red;
axis1 label=("Month");
axis2 label=(angle=90 "Dispensings") order=(0 to 40000 by 5000);
proc gplot data=quet3;
	plot (dispensings forecast)*month / overlay href='1jan2014'd lhref=4 chref="lightgray"
			haxis=axis1 vaxis=axis2; 
run; 
quit;
	 
